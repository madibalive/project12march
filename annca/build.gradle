apply plugin: 'com.android.library'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'me.tatarka.retrolambda'

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:1.4.1'
    }
}

group = 'io.github.memfis19'
version = '0.3.3'

ext {
    PUBLISH_GROUP_ID = 'io.github.memfis19'
    PUBLISH_ARTIFACT_ID = 'annca'
    PUBLISH_VERSION = '0.3.3'
    PUBLISH_VERSION_CODE = 7
    SUPPORT_VERSION = '25.0.0'
    TARGET_SDK = 24
    MIN_SDK = 16
    BUILD_TOOLS = "24.0.3"
}

android {
    compileSdkVersion TARGET_SDK
    buildToolsVersion BUILD_TOOLS

    defaultConfig {
        minSdkVersion MIN_SDK
        targetSdkVersion TARGET_SDK
        versionCode PUBLISH_VERSION_CODE
        versionName PUBLISH_VERSION

        manifestPlaceholders = [ appPackageName: "1836e88cc87844ce8cbbc5dfa7782e9f"]

        def addConstant = { constantName, constantValue ->
            buildConfigField "String", "${constantName}", "\"${constantValue}\""
        }
        addConstant("ADOBE_SECRET", "cbfd6c7f-7eba-4410-b184-5553d87c1bf3")
        addConstant("ADOBE_KEY", "1836e88cc87844ce8cbbc5dfa7782e9f")
        addConstant("ADOBE_URL", "ams+32e4653c9fb8974bdbb4b099d102801891528775://adobeid/1836e88cc87844ce8cbbc5dfa7782e9f")

    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions {
        abortOnError false
    }

    /* 2) Compile for Java 1.8 or greater */
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    /* 3) Exclude duplicate licenses */
    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/DEPENDENCIES'
        pickFirst 'AndroidManifest.xml'
    }

    /* 4) Enable jumbo mode */
    dexOptions {
        jumboMode true
        javaMaxHeapSize "4g"
    }
    buildToolsVersion '25.0.0'
}

task generateSourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier 'sources'
}

task generateJavadocs(type: Javadoc) {
    failOnError false
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath()
            .join(File.pathSeparator))
}

task generateJavadocsJar(type: Jar) {
    from generateJavadocs.destinationDir
    classifier 'javadoc'
}

generateJavadocsJar.dependsOn generateJavadocs

artifacts {
    archives generateSourcesJar
    archives generateJavadocsJar
}

install {
    repositories.mavenInstaller {
        pom.project {
            name 'io.github.memfis19'
            description 'Android solution to simplify work with different camera apis.'
            url 'https://github.com/memfis19/Annca'
            inceptionYear '2016'

            packaging 'aar'
            version PUBLISH_VERSION

            scm {
                connection 'https://github.com/memfis19/Annca.git'
                url 'https://github.com/memfis19/Annca'

            }
            developers {
                developer {
                    name 'memfis19'
                }
            }
        }
    }
}

bintray {
    Properties properties = new Properties()
    properties.load(project.rootProject.file('local.properties').newDataInputStream())
    user = properties.getProperty('bintray.user')
    key = properties.getProperty('bintray.apikey')
    configurations = ['archives']
    pkg {
        repo = 'io.github.memfis19'
        name = 'annca'
        userOrg = 'm-e-m-f-i-s'
        desc = 'Android solution to simplify work with different camera apis.'
        licenses = ['MIT']
        labels = ['android', 'camera', 'photo', 'video']
        websiteUrl = 'https://github.com/memfis19/Annca'
        issueTrackerUrl = 'https://github.com/memfis19/Annca/issues'
        vcsUrl = 'https://github.com/memfis19/Annca.git'
        version {
            name = PUBLISH_VERSION
            vcsTag = PUBLISH_VERSION
            desc = 'Minor fixes.'
            released = new Date()

        }
    }
}

dependencies {
    compile "com.android.support:support-v4:$SUPPORT_VERSION"
    compile "com.android.support:appcompat-v7:$SUPPORT_VERSION"
    androidTestCompile "com.android.support:support-annotations:$SUPPORT_VERSION"

    compile "com.android.support:design:$SUPPORT_VERSION"
    compile 'com.github.bumptech.glide:glide:3.7.0'


    compile fileTree(include: ['*.jar'], dir: 'libs')
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.4'
}